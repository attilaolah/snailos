load("@rules_foreign_cc//foreign_cc:configure.bzl", "configure_make")
load(":busybox_config.bzl", "busybox_config")

package(default_visibility = ["//pkg/busybox:__pkg__"])

# Absolute minimum configuration.
# This builds an empty BusyBox that prints "no applets enabled".
busybox_config(
    name = "config",
    base = ":base",
    values = {
        # Applets:
        "false": "y",
        "true": "y",
        "yes": "y",
    },
)

# Base BusyBox config.
# Adds any flags required for cross-compilation.
busybox_config(
    name = "base",
    base = ":empty",
    values = {
        # Removes GCC-specific flags.
        "use_portable_code": "y",
        # Required for proper off_t size detection.
        # Otherwise compilation fails with BUG_off_t_size_is_misdetected.
        "lfs": "y",
    },
)

# Absolute minimum configuration.
# This builds an empty BusyBox that prints "no applets enabled".
busybox_config(
    name = "empty",
    base = ":all_no",
    remove = [
        "install_applet_symlinks",
        "sh_is_ash",
        "shell_ash",
    ],
    values = {
        "install_applet_dont": "y",
        "sh_is_none": "y",
    },
)

# BusyBox's default "allnoconfig" output.
# This still builds the Almquist shell and installs it as a symlink.
configure_make(
    name = "all_no",
    args = ["HOSTCC=cc"],
    configure_in_place = True,
    lib_source = "@busybox_src//:all",
    out_data_dirs = ["config"],
    out_headers_only = True,
    postfix_script = "cp .config ${INSTALLDIR}/config/all_no.conf",
    targets = ["allnoconfig"],
)
